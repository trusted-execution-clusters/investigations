os := "fcos"

scos_base_img:= "quay.io/okd/scos-content@sha256:3813e6608a999756931d3d621932af9662860e71a552b2670f9fe320bf0d3585"
fcos_base_img:= "quay.io/fedora/fedora-coreos:42.20251012.2.0"

scos_img:= "quay.io/trusted-execution-clusters/scos"
fcos_img:= "quay.io/trusted-execution-clusters/fcos"

fcos_os:= "fedora-coreos"
scos_os:= "rhcos"

fcos_label:= "fedora-coreos"
scos_label:= "centos-stream-coreos"

base := if os == "scos" { scos_base_img } else { fcos_base_img }
image := if os == "scos" { scos_img } else { fcos_img }
os_name := if os == "scos" { scos_os } else { fcos_os }
label := if os == "scos" { scos_label } else { fcos_label }
archive := os + ".ociarchive"
platform := "qemu"


build:
    sudo podman build --no-cache --build-arg BASE={{base}} --build-arg COM_COREOS_OSNAME={{label}} -t {{image}} -f Containerfile .

oci-archive:
    sudo skopeo copy containers-storage:{{image}}:latest oci-archive:{{archive}}

osbuild:
    #!/bin/bash
    set -xeuo pipefail

    SELINUX_STATUS=$(getenforce)

    if [ "$SELINUX_STATUS" = "Enforcing" ]; then
        echo "WARNING: SELinux is in Enforcing mode. Temporarily disabling for osbuild operation..."
    fi

    TMPDIR=$(mktemp -d)
    git clone --depth 1 https://github.com/coreos/custom-coreos-disk-images ${TMPDIR}

    sudo -E ${TMPDIR}/custom-coreos-disk-images.sh --platform {{platform}} \
        --ociarchive {{archive}} \
        --osname {{os_name}}
    rm -rf "$TMPDIR"
    sudo chown $USER:$USER {{os}}-{{platform}}.x86_64.*

